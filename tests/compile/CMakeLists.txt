# separate implementation libraries to make sure the impls don't add 'header pollution'
fips_begin_lib(impl_sokol_app_gfx)
    fips_files(impl_sokol_app_gfx.c)
fips_end_lib()

fips_begin_lib(impl_sokol_gl)
    fips_files(impl_sokol_gl.c)
    fips_deps(impl_sokol_app_gfx)
fips_end_lib()

macro(add_system_libs)
    if (FIPS_IOS)
        fips_frameworks_osx(UIKit AudioToolbox AVFoundation)
        if (SOKOL_USE_METAL)
            fips_frameworks_osx(Metal MetalKit)
        else()
            fips_frameworks_osx(OpenGLES GLKit)
        endif()
    elseif (FIPS_MACOS)
        fips_frameworks_osx(Cocoa QuartzCore AudioToolbox)
        if (SOKOL_USE_METAL)
            fips_frameworks_osx(Metal MetalKit)
        else()
            fips_frameworks_osx(OpenGL)
        endif()
    elseif (FIPS_ANDROID)
        fips_libs(GLESv3 EGL OpenSLES log android)
    elseif (FIPS_LINUX)
        fips_libs(X11 Xi Xcursor GL m dl asound)
    endif()
endmacro()

# create a C and C++ (or ObjC and ObjC++) exe target
macro(compile_test name)
    if (NOT FIPS_UWP)
        fips_begin_app(${name}_c cmdline)
            fips_vs_warning_level(4)
            fips_files(${name}.c)
            add_system_libs()
            sokol_shader(shader.glsl ${slang})
        fips_end_app()
    endif()
    fips_begin_app(${name}_cc cmdline)
        fips_vs_warning_level(4)
        fips_files(${name}.cc)
        add_system_libs()
        sokol_shader(shader.glsl ${slang})
    fips_end_app()
    if (FIPS_OSX)
        target_compile_options(${name}_c -ObjC)
        target_compile_options(${name}_cc -ObjC++)
    endif()
endmacro()

macro(addlib name lib)
    if (NOT FIPS_UWP)
        target_link_libraries(${name}_c ${lib})
    endif()
    target_link_libraries(${name}_cc ${lib})
endmacro()

compile_test(sokol_gfx)
compile_test(sokol_glue)
compile_test(sokol_time)
compile_test(sokol_args)
compile_test(sokol_audio)
if (NOT FIPS_ANDROID AND NOT FIPS_UWP)
    compile_test(sokol_fetch)
endif()

compile_test(sokol_debugtext)
addlib(sokol_debugtext impl_sokol_app_gfx)

compile_test(sokol_fontstash)
addlib(sokol_fontstash impl_sokol_gl)

compile_test(sokol_gl)
addlib(sokol_gl impl_sokol_app_gfx)

compile_test(sokol_imgui)
addlib(sokol_imgui impl_sokol_app_gfx)
addlib(sokol_imgui cimgui)

compile_test(sokol_gfx_imgui)
addlib(sokol_gfx_imgui impl_sokol_app_gfx)
addlib(sokol_gfx_imgui cimgui)

compile_test(sokol_shape)
addlib(sokol_shape impl_sokol_app_gfx)

compile_test(sokol_nuklear)
addlib(sokol_nuklear impl_sokol_app_gfx)
